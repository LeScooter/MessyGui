buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'net.sf.proguard:proguard-gradle:6.2.2'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.2a"
group= "com.boehmod.messygui" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "mgui"

//
//Source Compatibility
//
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_20"
}

dependencies {

}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//
//Proguard Obfuscation-Pre Task
//
task buildAndObfuscate(type: proguard.gradle.ProGuardTask, dependsOn: build) {

    description "Obfuscates project after build task"
    group "build"

    injars jar.archivePath
    outjars "${jar.archivePath.absolutePath.replace(".jar", "")}-RELEASE.jar"

    // Add the main runtime jar and all non-shadowed dependencies
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    libraryjars "${System.getProperty('java.home')}/lib/jce.jar"

    String homeDir = System.getProperty("user.home") + "/"

    doFirst {
        sourceSets.main.compileClasspath
                .filter { !it.name.contains("Cobalt") }
                .each { libraryjars it }
    }

    // Add forge and Minecraft libraries
    libraryjars(homeDir + "/.gradle/caches/minecraft/net/minecraftforge/forge/1.8.9-11.15.1.2318-1.8.9/stable/20/forge-1.8.9-11.15.1.2318-1.8.9-srgBin.jar")

    //Setup proguard parameters
    dontusemixedcaseclassnames()
    dontshrink()
    dontoptimize()
    useuniqueclassmembernames()
    ignorewarnings()
    dontnote("com.goggle.**,javax.annotation.**,gnu.trove.**,com.sun.jna.**,cpw.mods.fml.**,net.minecraft.**")
    keepattributes("Exceptions,Signature,Deprecated,Synthetic,EnclosingMethod,RuntimeVisibleAnnotations,RuntimeInvisibleAnnotations,RuntimeVisibleParameterAnnotations,RuntimeInvisibleParameterAnnotations,AnnotationDefault,InnerClasses,*Annotation*")

    keep 'class !com.boehmod.** { *; }'

    dontwarn 'module-info'
    dontwarn 'org.apache.**,org.lwjgl.**'
}